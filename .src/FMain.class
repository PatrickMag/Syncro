' Gambas class file


Public imptimer As New Timer As "timer"     ''Timer pour importer les factures

Private $it As Boolean = True                 ''Test si le timer est en fonction pour ne pas passer 2 fois

Public Sub _new()

  Utils.Connect()
  Me.Raise
  Me.Text &= Utils.Societe
  Utils.numero = Settings["/Num"]
  Utils.clef = hexdblKey.Crypt("de", Settings["/Cle"], "Laurux")
  Utils.site = Settings["/Site"]
  Utils.transpor = Settings["/Trans"]
  If Settings["/Temps"] Then utils.temps = Settings["/Temps"] Else utils.temps = 0
  If IsNull(Utils.clef) Then
    cle_Click
  Endif
  If IsNull(Utils.transpor) Then
    Transport_Click()
  Endif
  If IsNull(Utils.numero) Then
    ncom_Click
  Endif
  If IsNull(Utils.site) Then
    siteint_Click()
  Endif
  
End

Public Sub cle_Click()
  
  Repeat
    Utils.clef = InputBox("Entrez la clef : ", "Clef API Régicom", Utils.clef)
  Until Utils.clef
  Settings["/Cle"] = hexdblKey.crypt("en", Utils.clef, "Laurux")
  Settings.Save
  
End

Public Sub ncom_Click()

  Repeat
    Utils.numero = InputBox("Entrez le dernier numéro importé : ", "Numéro de commande", Utils.numero)
  Until Utils.numero
  Settings["/Num"] = Utils.numero
  Settings.Save
  
End

Public Sub Transport_Click()

  Dim res As Result
  
  Repeat
    Utils.transpor = InputBox("Entrez le code de l'article transport : ", "Transport", Utils.transpor)
  Until Utils.transpor
  res = db.Exec("SELECT * FROM Fiches_Art WHERE art_code = &1", Utils.transpor)
  If Not res.Available Then
    Message.Error("Article innexistant", "OK")
    Transport_Click()
    Return
  Endif
  Settings["/Trans"] = Utils.transpor
  Settings.Save

End

Public Sub siteint_Click()

  Repeat
    Utils.site = InputBox("Entrez le nom du site (ex : https://www.exemple.com) : ", "Site internet", Utils.site)
  Until Utils.site
  Settings["/Site"] = Utils.site
  Settings.Save

End

Public Sub Timer_Click()

  Dim tm As String
  
  Repeat
    tm = InputBox("Durée du timer : ", "Timer", utils.temps)
    If Not tm Then Return
  Until Val(tm)
  Settings["/Temps"] = Val(tm)
  Settings.Save

End

Public Sub Quit_Click()

  Me.Close

End

Public Sub bsyncro_Click()

  Dim sync As Syncro
  Dim win As Window
  Dim pan As Panel
  Dim lab As Label
  
  mab()
  If btimer.Background = Color.Red Then
    Message.Info("Les fichiers nécessaires à l'importaion des écritures vont êtres bloqués.\nVeuillez fermer Laurux sur tous les postes.", "OK")
  Endif
  win = New Window
  win.h = Me.h / 2
  win.w = Me.w / 2
  win.x = Me.y - Me.W
  win.y = 0
  win.Arrangement = Arrange.Vertical
  lab = New Label(win)
  lab.AutoResize = True
  lab.Text = "Les fichiers nécessaires à l'importaion des factures vont êtres bloqués."
  Me.Raise
  win.Show
  Music.Load("warning.ogg")
  If btimer.Background = Color.Green Then Music.Play(3)
  Wait 0.001
  Utils.db.Exec("LOCK TABLES Fiches_Art WRITE, Fiches_Cli WRITE, Fiches_Bl WRITE, Fiches_Ligbl WRITE, Fiches_Parametres WRITE, Fiches_Comptes WRITE, Fiches_Tvaav WRITE")
  lab = New Label(win)
  lab.AutoResize = True
  lab.Text = "Les fichiers sont bloqués."
  ProgressBar1.Value = 0
  sync = New Syncro
  If TestCom(sync) Then Return 
  ProgressBar1.Value = 1 / 3
  Wait 0.001
  If sync.import(import) Then 
    Utils.db.Exec("UNLOCK TABLES")
    win.Delete
    Return
  Endif
  ProgressBar1.Value = 2 / 3
  Wait 0.001
  If sync.TestArt(TextArea1) Then
    articles.Text = "Vérication articles : KO"
    Utils.db.Exec("UNLOCK TABLES")
    win.Delete
    Return
  Else
    articles.Text = "Vérication articles : OK"
    Wait 0.001
  Endif
  ProgressBar1.Value = 3 / 3
  Wait 0.001
  If sync.integration(maj) Then
    integration.Text = "integration : KO"
    Utils.db.Exec("UNLOCK TABLES")
    win.Delete
    Return
  Else
    integration.Text = "Integration : OK"
    Wait 0.001
  Endif
  
  utils.db.Exec("UNLOCK TABLES")
  win.Delete
  Application.Busy = 0
  $it = True
  
End

Public Sub Vart_Click()       ''Vérification des articles

  Dim sync As Syncro
  
  mab()
  sync = New Syncro
  If TestCom(sync) Then Return
  sync.VerifArt(ProgressBar1)

End

Public Sub Blst_Click()     ''Liste des articles

  Dim sync As Syncro
  
  mab()
  sync = New Syncro
  If TestCom(sync) Then Return
  sync.VerifArt(ProgressBar1, True)

End

Public Sub Bmaj_Click()

  Dim sync As Syncro
  
  mab()
  sync = New Syncro
  If TestCom(sync) Then Return
  If sync.maj(stk.Value, prx.Value, ProgressBar1, TextArea1) Then
    maj.Text = "Mise à jour KO"
  Endif

End

Public Sub Bimpor_Click()

  Dim sync As New Syncro
  
  mab()
  If TestCom(sync) Then Return
  Application.Busy = 0
  sync.ImpCsv(Me.Height, Me.Width, ProgressBar1, TextArea1)
  
End

Private Function TestCom(sync As Syncro) As Boolean
  
  If btimer.Background = Color.Green And $it Then 
    test.Text = "Le timer est sur mode automatique : KO"
    utils.db.Exec("UNLOCK TABLES")
    Return True
  Endif
  Application.Busy = 1
  If sync.test() Then 
    test.Text = "Communication : KO"
    utils.db.Exec("UNLOCK TABLES")
    Application.Busy = 0
    Return True
  Endif
  test.Text = "Communication : OK"
  Wait 0.001
  Return False
  
End

Public Sub btimer_Click()

  If btimer.Background = Color.Red Then
    btimer.Background = Color.Green
    btimer.Text = "Timer\nOn"
    imptimer.Delay = utils.temps * 1000
    imptimer.Enabled = True
    imptimer.Start
  Else
    imptimer.Stop
    btimer.Background = Color.Red
    btimer.Text = "Timer\nOff"
  Endif

End

Public Sub timer_Timer()

  If Not $it Then Return

  $it = False
  bsyncro_Click()

End

Private Sub mab()
  
  TextArea1.Clear
  ProgressBar1.Value = 0 / 3
  test.Text = ""
  articles.Text = ""
  integration.Text = ""
  maj.Text = ""
  Wait 0.001
  
End
